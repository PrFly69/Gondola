actor ReboundStrikerWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons
//$Title Rebound Striker
//$Sprite STRIA0
Weapon.AmmoUse 3
Weapon.AmmoGive 33
Weapon.SlotNumber 2
Obituary "$OB_REBOUNDSTRIKER"
Tag "$TAG_REBOUNDSTRIKER"
Inventory.Pickupmessage "$PU_REBOUNDSTRIKER"
weapon.ammotype "ReboundStrikerAmmo"
inventory.icon "STRIKI"
States
{
SpawnLoop:
STRI A -1
loop
Ready:
RSHD A 0 ACS_ExecuteAlways(998,0,200)
RSHD A 0 A_JumpIfNoAmmo("ReadyNoAmmo")
RSHD A 1 A_WeaponReady
Goto Ready+1
ReadyNoAmmo:
RSHD J 1 A_WeaponReady
RSHD J 0 A_JumpIfNoAmmo("ReadyNoAmmo")
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
RSHD A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
RSHD A 1 A_Raise
Loop
Fire:
RSHD A 0 A_JumpIfNoAmmo("NoAmmo")
RSHD A 0 A_PlaySoundEx("weapon/ReboundStrikerCWep", "weapon")
RSHD A 0 A_FireCustomMissile("ReboundStrikerCWep", 0, 1, 8, -10)
RSHD BCDEFG 2
TNT1 A 13
RSHD HI 2
RSHD A 0 A_Refire
Goto Ready+1
NoAmmo:
RSHD J 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor ReboundStrikerSloppywep
{
PROJECTILE
+FORCEXYBILLBOARD
+HEXENBOUNCE
+CANBOUNCEWATER
-ALLOWBOUNCEONACTORS
+USEBOUNCESTATE
+FORCEXYBILLBOARD
Obituary "$OB_REBOUNDSTRIKER"
damagetype "ReboundStriker"
seesound "weapon/reboundstriker"
Radius 10
Height 20
scale 2.5
speed 40
bouncecount 3
wallbouncefactor 0.91
bouncefactor 0.91
var int user_bouncenum;
damage (30+user_bouncenum)
States
{
Spawn:
STRI BC 2
loop
Bounce:
STRI B 1 A_SetUserVar(user_bouncenum, user_bouncenum+15)
STRI B 0 A_JumpIf(user_bouncenum > 15, 2)
goto Spawn2
STRI B 0
STRI B 0
goto Spawn3
Spawn2:
STRI DF 2
loop
Spawn3:
STRI EG 2 BRIGHT
loop
Death:
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1",0,0,10)
stop
}
}

Actor ReboundStrikerCWep
{
Translation "198:198=251:251", "192:192=38:38"
PROJECTILE
Damagetype "ClassPainLess2"
Obituary "%o was smacked to the benches by %k's Rebound Striker."
+DONTSPLASH
+HEXENBOUNCE
+SKYEXPLODE
+BOUNCEONACTORS
+USEBOUNCESTATE
+HITTRACER
SeeSound "weapon/reboundprojectile"
BounceFactor 1.0
WallBounceFactor 1.0
BounceCount 7
Damage (15)
Radius 16
Height 16
Speed 50
Scale 2.5
States 
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteAlways("BULL_Tidifier", 0)
Spawn2:
RBDS ABCD 1
loop

Bounce.Actor:
RBDS A 0 A_JumpIf(ACS_NamedExecuteWithResult("cbm_istracerenemy")==1,"AntiRape")
RBDS A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TARGET)
goto Spawn2

AntiRape:
RBDS A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TARGET)
RBDS A 0 A_ChangeFlag("THRUACTORS", 1)
RBDS A 1 A_ChangeVelocity(50.0,0,velz,CVF_RELATIVE|CVF_REPLACE)
RBDS B 1
RBDS C 1
RBDS D 1
RBDS D 0 A_ChangeFlag("THRUACTORS",0)
goto Spawn2

Death:
TNT1 A 1
stop
}
}