actor BlackHoleWep : BaseMM8BDMWep
{
    //$Category MM8BDM-Weapons
    //$Title Black Hole
    //$Sprite BHOLA0
    Weapon.AmmoUse 4
    Weapon.AmmoGive 28
    Weapon.SlotNumber 7
    Inventory.Pickupmessage "$PU_BLACKHOLE"
    Obituary "$OB_BLACKHOLE"
    Tag "$TAG_BLACKHOLE"
    weapon.ammotype "BlackHoleAmmo"
    inventory.icon "BHOLWI"
    States
    {
    SpawnLoop:
        BHOL A -1
        loop
    Ready:
        BHOL I 0 ACS_ExecuteAlways(998,0,211)
        BHOL I 1 A_WeaponReady
        Goto Ready+1
    Deselect:
        TNT1 A 0 A_JumpIfInventory("ShieldCheck", 1, "Detonate")
        TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
        BHOL I 1 A_Lower
        Loop
    Select:
        TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
        BHOL I 1 A_Raise
        Loop
    Fire:
        BHOL I 0 A_JumpIfInventory("ShieldCheck", 1, "Detonate")
        BHOL I 0 A_JumpIfNoAmmo("NoAmmo")
        BHOL I 0 A_JumpIfInventory("BlackHoleTrigger", 1, "NoAmmo")
        TNT1 A 2
        BHOL LM 1
        BHOL I 0 A_PlaySoundEx("weapon/blackholestart","Weapon")
        BHOL I 0 A_GiveInventory("ShieldCheck", 1)
        BHOL I 0 A_GiveInventory("SpawnBlackHole", 1)
        BHOL NOP 1
        TNT1 A 2
        BHOL JK 2
    Active:
        BHOL KKKKKKKKKKKKKKKKKK 1 A_WeaponReady
        BHOL K 0 A_TakeInventory("BlackHoleAmmo", 1, TIF_NOTAKEINFINITE)
        BHOL K 0 A_JumpIfInventory("BlackHoleAmmo", 1, "Active")
        goto Detonate
    Detonate:
        BHOL I 0 A_PlaySoundEx("weapon/blackholestart","Weapon")
        BHOL I 0 A_GiveInventory("BlackHoleTrigger", 1)
        BHOL I 0 A_TakeInventory("ShieldCheck", 1)
        BHOL I 0 A_TakeInventory("BlackHoleAmmo", 4,TIF_NOTAKEINFINITE)
        BHOL QR 1
        BHOL S 10
        BHOL RQ 1
        BHOL I 10
        //BHOL I 0 A_TakeInventory("BlackHoleTrigger", 1)
        BHOL I 0 A_ClearRefire
        goto Ready+1
    NoAmmo:
        BHOL I 1 ACS_Execute(979,0)
        Goto Ready+1
    }
}

// actor BlackHoleTrigger : Once {}

actor SpawnBlackHole : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_SpawnItemEx("BlackHoleSpawner", 0, 0, 32)
        TNT1 A 0 A_JumpIfInventory("RuneSpread", 1, "PickupSpread")
        stop
    PickupSpread:
        TNT1 A 0 A_SpawnItemEx("BlackHoleSpawner", 64, 0, 32, 0, 0, 0, 0)
        TNT1 A 0 A_SpawnItemEx("BlackHoleSpawner", 64, 0, 32, 0, 0, 0, 120)
        TNT1 A 0 A_SpawnItemEx("BlackHoleSpawner", 64, 0, 32, 0, 0, 0, 240)
        stop
    }
}

actor BlackHoleTrigger : Powerup
{
    powerup.duration 35
}

actor BlackHoleSpawner
{
    radius 1
    height 1
    +MISSILE
    +NOINTERACTION
    var int user_part;
    var int user_InFloor;
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TARGET)
        TNT1 A 0 A_SetUserVar(user_InFloor, (z-floorz < 32))
        TNT1 A 0 A_SpawnItemEx("BlackHoleCore", 0, 0, !user_InFloor * -32 + user_InFloor * -(z-floorz), 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
        TNT1 A 0 A_SetUserVar(user_part, BLACKHOLE_PARTMAX)
    SpawnWait:
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"End")
        TNT1 A 1 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        loop
    Detonate:
        TNT1 A 0 A_PlaySoundEx("weapon/blackholestart","auto")
        TNT1 A 15
        TNT1 A 0 A_PlaySoundEx("weapon/blackholeexplode","auto")
        TNT1 A 1
    SpawnSpiral:
        TNT1 A 0 A_SpawnItemEx("BlackHoleSpiral", 0, 0, user_InFloor * (z-floorz), 1, 0, 0, user_part*(360/BLACKHOLE_PARTMAX))
        TNT1 A 0 A_SetUserVar(user_part, user_part-1)
        TNT1 A 0 A_JumpIf(user_part>0, "SpawnSpiral")
        TNT1 A 1 A_JumpIf(true, "End")
        wait
    End:
        TNT1 A 0
        stop
    }
}

actor BlackHoleCore
{
    +NOGRAVITY
    +ISMONSTER
    +NOTARGETSWITCH
    +THRUACTORS
    +FORCEXYBILLBOARD
    +MOVEWITHSECTOR
    +BRIGHT
    height 1
    radius 1
    maxstepheight 0
    renderstyle translucent
    alpha 0.8
    Scale 2.5
    var int user_part;
    var int user_partmax;
    States
    {
    Spawn:
        BHOL B 0
        BHOL G 0 A_RearrangePointers(AAPTR_TRACER, AAPTR_DEFAULT, AAPTR_DEFAULT)
        BHOL B 0 A_SetUserVar(user_partmax, 16)
        BHOL B 0 A_SetUserVar(user_part, user_partmax)
    SpawnParts:
        BHOL B 0 A_SpawnItemEx("BlackHolePart", 0, 0, 0, 0, 0, 0, user_part*(360/BLACKHOLE_PARTMAX), SXF_SETMASTER|SXF_TRANSFERPOINTERS)
        BHOL B 0 A_SetUserVar(user_part, user_part-1)
        BHOL B 0 A_JumpIf(user_part<=0, "Active")
        BHOL B 1 A_JumpIf(true, "SpawnParts")
        wait
    Active:
        BHOL BBBB 1 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        BHOL B 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"End")
        BHOL CCCC 1 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        BHOL C 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"End")
        BHOL DDDD 1 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        BHOL D 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"End")
        BHOL EEEE 1 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        BHOL E 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"End")
        BHOL FFFF 1 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        BHOL F 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"End")
        loop
    Detonate:
        BHOL A 0 A_KillChildren
        BHOL BCDEFBCDEFBCDEF 1
        stop
    End:
        TNT1 A 0
        stop
    }
}

actor BlackHolePart
{
    +ISMONSTER
    +NOCLIP
    +NOGRAVITY
    +DROPOFF
    +MISSILE
    +ACTIVATEIMPACT
    +ACTIVATEPCROSS
    +SHOOTABLE
    +DONTREFLECT
    +DONTSPLASH
    +DONTBLAST
    +DONTRIP
    +INVULNERABLE
    +NOBLOOD
    +FORCEXYBILLBOARD
    +MOVEWITHSECTOR
    +NOTAUTOAIMED
    +BRIGHT
    painchance 255
    maxstepheight 0
    Scale 2.5
    height 56
    radius 38
    var int user_radius;
    var int user_angle;
    var int user_angleadd;
    var int user_expandtime;
    States
    {
    Spawn:
        BHOL G 0
        BHOL G 0 A_SetUserVar(user_radius, 32)
        BHOL G 0 A_SetUserVar(user_angleadd, 12)
        BHOL G 0 A_SetUserVar(user_expandtime, 40)
        BHOL G 0 A_SetUserVar(user_angle, angle)
        goto Expand
    SpawnLoop:
        BHOL H 0 A_JumpIf(ACS_NamedExecuteWithResult("core_targetexists")==0,"End")
        BHOL H 0 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        BHOL G 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        BHOL G 1 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, 0, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION) 
        BHOL H 0 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        BHOL G 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        BHOL G 1 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, 0, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)  
        BHOL H 0 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        BHOL H 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        BHOL H 1 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, 0, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
        BHOL H 0 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        BHOL H 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        BHOL H 1 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, 0, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)  
        loop
    Expand:
        BHOL H 0 A_SetUserVar(user_angleadd, user_angleadd-((user_expandtime%4)%2))
		BHOL H 0 A_SetUserVar(user_radius, user_radius+4)
		BHOL H 0 A_SetUserVar(user_expandtime, user_expandtime-1)
		BHOL H 1
		BHOL H 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
		BHOL H 0 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, 0, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
		BHOL H 0 A_JumpIf(user_expandtime <= 0, "SpawnLoop")
        BHOL H 0 A_JumpIfInTargetInventory("BlackHoleTrigger", 1, "Detonate")
        loop
    Death:
    Detonate:
        BHOL H 0 A_SetUserVar(user_radius, user_radius-16)
        BHOL H 1
        BHOL H 0 A_SetUserVar(user_angle, user_angle-user_angleadd)
        BHOL H 0 A_Warp(AAPTR_MASTER, cos(-user_angle)*user_radius, sin(-user_angle)*user_radius, 0, 0, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
        BHOL H 0 A_JumpIf(user_radius <= 0, "End")
        loop
    End:
        BHOL H 0
        stop
    }
}

actor BlackHoleSpiral
{
    PROJECTILE
    +FORCEXYBILLBOARD
    +BRIGHT
    Radius 16
    Height 10
    scale 2.5
    damage (20)
    damagetype "BlackHole"
    Obituary "$OB_BLACKHOLE"
    speed 0
    var int user_speed;
    var int user_maxturn;
    var int user_turndivisor;
    var int user_turndivisor_add;
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SetUserVar("user_speed", 50)
        TNT1 A 0 A_SetUserVar("user_maxturn", 1500)
        TNT1 A 0 A_SetUserVar("user_turndivisor", 80)
        TNT1 A 0 A_SetUserVar("user_turndivisor_add", 10)
    Spawn2:
        TNT1 A 0 A_Setangle(angle-user_maxturn/user_turndivisor)
        BHOL Y 1 A_ChangeVelocity(user_speed,0,0,CVF_RELATIVE|CVF_REPLACE)
        TNT1 A 0 A_SetUserVar("user_turndivisor",user_turndivisor+user_turndivisor_add)
        TNT1 A 0 A_Setangle(angle-user_maxturn/user_turndivisor)
        BHOL Y 1 A_ChangeVelocity(user_speed,0,0,CVF_RELATIVE|CVF_REPLACE)
        TNT1 A 0 A_SetUserVar("user_turndivisor",user_turndivisor+user_turndivisor_add)
        TNT1 A 0 A_Setangle(angle-user_maxturn/user_turndivisor)
        BHOL Z 1 A_ChangeVelocity(user_speed,0,0,CVF_RELATIVE|CVF_REPLACE)
        TNT1 A 0 A_SetUserVar("user_turndivisor",user_turndivisor+user_turndivisor_add)
        TNT1 A 0 A_Setangle(angle-user_maxturn/user_turndivisor)
        BHOL Z 1 A_ChangeVelocity(user_speed,0,0,CVF_RELATIVE|CVF_REPLACE)
        TNT1 A 0 A_SetUserVar("user_turndivisor",user_turndivisor+user_turndivisor_add)
        loop
    Death:
        POWS A 0
        stop
    }
}
