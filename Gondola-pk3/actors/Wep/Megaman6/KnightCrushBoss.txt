Actor KnightCrushBoss : BaseMM8BDMWep_CBM {
	Tag "$TAGC_6D"
	Dropitem "KnightCrushWepCDropped"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 28
	Obituary "$OB_KNIGHTCRUSH"
	Weapon.ammotype "KnightmanAmmo"
	+WEAPON.NOAUTOAIM
	States {
		Spawn:
			C_06 D 1
		Loop
		Ready:
			KNIA A 1 ACS_ExecuteAlways(998, 0, DYE_KNIGHTMAN)
			TNT1 A 0 A_GiveInventory("KnightmanShieldSpawner", 1)
		ReadyNext:
			KNIA A 0 A_TakeInventory("StopShield", 9)
		Goto Ready1
		Ready1:
			KNIA A 0 A_JumpIfInventory("KnightmanAmmo", 28, "Ready2")
			KNIA A 3 A_WeaponReady(8)
			KNIA A 0 A_GiveInventory("KnightmanAmmo", 1)
		Loop
		Ready2:
			KNIA A 3 A_WeaponReady
			KNIA A 0 A_GiveInventory("KnightmanAmmo", 1)
		Loop
		Deselect:
			KNIA A 0
			TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
			KNIA A 1 A_Lower
		Loop
		Select:
			TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
			KNIA A 1 A_Raise
		Loop
		Fire:
			KNIA B 0 
			KNIA B 0 A_GiveInventory("StopShield",1)
			KNIA P 1 A_GiveInventory("KnightmanFlag",1)
			KNIA H 1
			KNIA F 1
			KNIA C 0 A_TakeInventory("KnightDouble",1)
			KNIA W 1 
			KNIA C 1 A_PlaySoundEx("weapon/KnightShot","Weapon")
			KNIA D 2 A_GiveInventory("KnightmanMain_CI",1)
			KNIA E 2
			KNIA T 4
		Goto Check
		Check:
			KNIA D 0 A_JumpIfInventory("KnightDouble",1,"Return")
			KNIA U 3 A_GiveInventory("KnightFlag",1)
			KNIA D 0 //A_GiveInventory("KnightmanAmmo", 1)
			KNIA D 0 A_JumpIfInventory("KnightFlag",12,"Return")//close enough to checking per tic 35 times
		loop
		Return:
			KNIA H 2 A_TakeInventory("KnightmanFlag",999)
			KNIA P 2
			KNIA A 2 A_TakeInventory("KnightFlag",999)
			KNIA A 2
		Goto ReadyNext
		AltFire:
			KNIA A 1 A_JumpIfInventory("KnightmanAmmo", 28, "ShieldCharge")
		Goto ReadyNext
		ShieldCharge:
			KNIA G 0 A_GiveInventory("VivifyDelay7",1)
			KNIA G 0 A_Refire(1)
			KNIA G 0 A_TakeInventory("KnightmanAmmo",99)
			KNIA G 0 A_PlaySoundEx("weapon/chargekick","Weapon")
			KNIA Q 1 SetPlayerProperty(0,1,0)
			KNIA N 1
			KNIA G 0 A_GiveInventory("StopShield",1)
			KNIA G 0 A_GiveInventory("InvulnerableOn_Pickup",1)
			KNIA O 1 A_ChangeVelocity(Cos(pitch)*6,0,-sin(pitch)*3,1)//A_Recoil(-6)
			KNIA H 0 A_FireCustomMissile("KnightRushX")
			KNIA H 0 A_GiveInventory("Repulsion",1)
			KNIA O 1 A_ChangeVelocity(Cos(pitch)*6,0,-sin(pitch)*3,1)
			KNIA H 0 A_FireCustomMissile("KnightRush")
			KNIA H 0 A_GiveInventory("Repulsion",1)
			KNIA O 1 A_ChangeVelocity(Cos(pitch)*6,0,-sin(pitch)*3,1)
			KNIA H 0 A_FireCustomMissile("KnightRush")
			KNIA H 0 A_GiveInventory("Repulsion",1)
			KNIA O 1 A_ChangeVelocity(Cos(pitch)*6,0,-sin(pitch)*3,1)
			KNIA H 0 A_FireCustomMissile("KnightRush")
			KNIA H 0 A_GiveInventory("Repulsion",1)
			KNIA O 1 SetPlayerProperty(0,0,0)
			KNIA G 0 A_GiveInventory("InvulnerableOff_Pickup",1)
			KNIA G 0 A_TakeInventory("StopShield",9)
			KNIA O 1 A_GunFlash
			KNIA NQ 1
		Goto Ready1
	}
}

Actor KnightmanShieldSpawner : CustomInventory {
	+ALWAYSPICKUP
	States {
		Pickup:
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox_1")
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox_2")
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox_3")
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox_4")
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox_5")
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox_6")
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox_7")
		Stop
	}
}

Actor KnightmanAmmo : Ammo {
	Inventory.amount 1
	Inventory.maxamount 28
	+INVENTORY.IGNORESKILL
}

Actor KnightmanFlag : OnceC { }

Actor KnightmanCrusher {
	Translation "198:198=75:75"
	PROJECTILE
	Damagetype "ClassPainLess2"
	Obituary "$OB_KNIGHTCRUSH"
	+RIPPER
	+DONTSPLASH
	+HEXENBOUNCE
	+SKYEXPLODE
	+THRUGHOST
	BounceFactor 1.0
	WallBounceFactor 1.0
	Damage (0)
	Radius 8
	Height 8
	Speed 40
	Scale 2.5
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("BULL_Tidifier", 0)
			SpawnLOOP:
			KNIA I 0
			KNIA II 1 A_SpawnItemEx("KnightmanCrusherDamager", 0, 0, 0, momx, momy, momz, 0, 8, 0)
			KNIA I 0 A_ChangeFlag("THRUGHOST", 0)
			KNIA IIIIIIII 1 A_SpawnItemEx("KnightmanCrusherDamager", 0, 0, 0, momx, momy, momz, 0, 8, 0)
			KNIA I 0 A_TakeFromTarget("KnightFlag", 999)
			KNIA IIIIIIII 1 A_SpawnItemEx("KnightmanCrusherDamager", 0, 0, 0, momx, momy, momz, 0, 8, 0)
			KNIA I 0 A_SpawnItemEx("KnightmanCrusherReturn", 0, 0, 0, 0, 0, 0, 0, 1)
		Stop
		Death:
			KNIA I 0 A_GiveToTarget("KnightDouble", 1)
			KNIA I 0 A_TakeFromTarget("KnightFlag", 999)
		Stop
	}
}

Actor KnightmanCrusherB : KnightmanCrusher {
	Translation "198:198=74:74"
}

Actor KnightmanCrusherR : KnightmanCrusher {
	Translation "198:198=41:41"
}

Actor KnightmanCrusherO : KnightmanCrusher {
	Translation "198:198=128:128"
}

Actor KnightmanCrusherP : KnightmanCrusher {
	Translation "198:198=232:232"
}

Actor KnightReturnerSound : BasicHelper {
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_PlaySoundEx("weapon/KnightReturn", "Weapon")
		Stop
	}
}

Actor KnightmanCrusherReturn : KnightmanCrusher {
	+NOINTERACTION
	+SEEKERMISSILE
	ReactionTime 87//about 10 seconds
	States {
		Spawn:
			KNIA I 0
			KNIA I 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_DEFAULT, AAPTR_TARGET)//Target >> Tracer
			KNIA I 0 A_FaceTarget
		Goto Spawn2
		Spawn2:
			KNIA I 0 A_TakeFromTarget("KnightFlag", 99)
			KNIA I 0 A_JumpIfCloser(46, "Death")
			KNIA I 0 A_SpawnItemEx("KnightReturnerSound")
			KNIA II 0 A_SeekerMissile(90, 90, SMF_PRECISE)
			KNIA I 1 A_SpawnItemEx("KnightmanCrusherDamager", 0, 0, 0, momx, momy, momz, 0, 8)
			KNIA I 0 A_JumpIfCloser(46, "Death")
			KNIA I 1 A_SpawnItemEx("KnightmanCrusherDamager", 0, 0, 0, momx, momy, momz, 0, 8)
			KNIA I 0 A_JumpIfCloser(46, "Death")
			KNIA I 1 A_SpawnItemEx("KnightmanCrusherDamager", 0, 0, 0, momx, momy, momz, 0, 8)
			KNIA I 0 A_JumpIfCloser(46, "Death")
			KNIA I 1 A_SpawnItemEx("KnightmanCrusherDamager", 0, 0, 0, momx, momy, momz, 0, 8)
			KNIA I 0 A_JumpIfCloser(46, "Death")
			KNIA I 0 A_CountDown
			KNIA I 0 A_JumpIf(ACS_ExecuteWithResult(C_CHECK_TARGET_OOC)==0, "Death")
		Loop
	}
}

Actor KnightmanCrusherDamager {
	PROJECTILE
	Damagetype "ClassPainLess2"
	Obituary "$OB_KNIGHTCRUSH"
	+RIPPER
	+DONTBLAST
	Damage (8)//11
	Radius 12
	Height 12
	States {
		Spawn:
			TNT1 A 2
		Stop
	}
}

Actor KnightShieldX : BasicHelper {
	+NOTIMEFREEZE
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox", 0, -24, 0, velx, vely, velz, 0, 8)
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox", 0, -16, 0, velx, vely, velz, 0, 8)
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox", 0, -8, 0, velx, vely, velz, 0, 8)
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox", 0, 0, 0, velx, vely, velz, 0, 8)
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox", 0, 8, 0, velx, vely, velz, 0, 8)
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox", -8, 8, 0, velx, vely, velz, 0, 8)
			TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox", -16, 8, 0, velx, vely, velz, 0, 8)
			TNT1 A 5
		Stop
	}
}

Actor NewProtoShieldHitbox {
	+MISSILE
	Speed 0
	+DONTSPLASH
	Damage 0
	Height 42
	Radius 5
	Health 9999
	Scale 2.5
	Damagefactor "ProtoBuster", 1.0
	+SHOOTABLE
	+NOBLOOD
	-SOLID
	+NOTIMEFREEZE
	+NOTARGETSWITCH
	+NOGRAVITY
	+HEXENBOUNCE
	+BOUNCEONACTORS
	Painchance 256
	Bloodtype ""
	+DONTRIP
	+GHOST
	+NORADIUSDMG
	+DONTBLAST
	+DONTDRAIN
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 2
		Stop
		Death:
			TNT1 A 0
			TNT1 A 1
		Stop
		Pain:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("ProtoShieldDeahthFX")
			TNT1 A 1 A_PlaySound("item/protoreflect")
		Stop
	}
}

Actor KnightShieldHitbox : NewProtoShieldHitbox {
	+NOCLIP
	States {
		Pain:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("KnightShieldDeathFX")
			TNT1 A 2 A_PlaySound("item/protoreflect")
		Goto Spawn
	}
}

Actor KnightShieldHitbox_1 : KnightShieldHitbox {
	Meleerange 28
	Accuracy -9
	Mass 8
    States {
        Spawn:
            TNT1 A 0
            TNT1 AA 1 A_Warp(AAPTR_TARGET, Meleerange, Accuracy, Mass, 0, WARPF_NOCHECKPOSITION)
            TNT1 A 0 A_JumpIfInTargetInventory("StopShield", 1, "UnsetShootable")
            TNT1 A 0 A_JumpIf(!ACS_NamedExecuteWithResult("CCBM Knight Shield Check"), "Death")
            TNT1 A 0 A_ChangeFlag("SHOOTABLE", 1)
        Loop
        UnsetShootable:
            TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
        Goto Spawn
        Pain:
            TNT1 A 0
            TNT1 A 0 A_SpawnItemEx("KnightShieldDeathFX")
            TNT1 A 1 A_PlaySound("item/protoreflect")
        Goto Spawn
    }
}
Actor KnightShieldHitbox_2 : KnightShieldHitbox_1 {
    Accuracy 0
}
Actor KnightShieldHitbox_3 : KnightShieldHitbox_1 {
    Accuracy 9
}
Actor KnightShieldHitbox_4 : KnightShieldHitbox_1 {
    Accuracy 18
}
Actor KnightShieldHitbox_5 : KnightShieldHitbox_1 {
    Accuracy 27
}
Actor KnightShieldHitbox_6 : KnightShieldHitbox_1 {
	Meleerange 20
    Accuracy 27
}
Actor KnightShieldHitbox_7 : KnightShieldHitbox_1 {
    Meleerange 12
	Accuracy 27
}

Actor KnightShieldDeathFX : BasicClientSide {
	Alpha 0.9
	Renderstyle add
	States {
		Spawn:
			KNIA L 0 A_SetScale(scaleX + 0.1, 2.5)
			KNIA L 1 A_FadeOut(0.05)
		Loop
	}
}

Actor KnightRush : BasicExplosion {
	Damagetype "KnightRush"
	Obituary "$OB_KNIGHTRUSH"
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_Explode(5, 120, 0, 0, 16)
		Stop
	}
}

Actor KnightRushX : KnightRush {
	Obituary "$OB_KNIGHTRUSHX"
}

Actor KnightmanMain_CI : TeamColor_CI {
	States {
		FireX:
			TNT1 A 0 A_FireCustomMissile("KnightmanCrusher", 0, -8, 0, 0)
		Goto Done
		FireB:
			TNT1 A 0 A_FireCustomMissile("KnightmanCrusherB", 0, -8, 0, 0)
		Goto Done
		FireR:
			TNT1 A 0 A_FireCustomMissile("KnightmanCrusherR", 0, -8, 0, 0)
		Goto Done
		FireO:
			TNT1 A 0 A_FireCustomMissile("KnightmanCrusherO", 0, -8, 0, 0)
		Goto Done
		FireP:
			TNT1 A 0 A_FireCustomMissile("KnightmanCrusherP", 0, -8, 0, 0)
		Goto Done
	}
}